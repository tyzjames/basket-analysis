USE SCHEMAXXX

-- GET THE SUPPORT, CONFIDENCE AND LIFT FOR A SINGLE ITEM

-- SELECT ITEM TO ANALYSE
DECLARE @ITEM AS INT
SET @ITEM = 2184

	DECLARE @TOTAL_TXN_COUNT_ALL AS INT
	DECLARE @TOTAL_TXN_COUNT AS INT
	SET @TOTAL_TXN_COUNT_ALL = (SELECT COUNT(DISTINCT TXN_ID) FROM dbo.BA_Transactions)
	SET @TOTAL_TXN_COUNT = (SELECT COUNT(DISTINCT TXN_ID) FROM dbo.BA_Transactions WHERE ITEM_ID IN (@ITEM))


	-- GET ITEM PAIR WITH TARGET ITEM
	---------------------------------------------------------------------------------------------------------------------
	IF OBJECT_ID('TEMPDB..#ITEM_PAIR') IS NOT NULL
		DROP TABLE #ITEM_PAIR
	CREATE TABLE #ITEM_PAIR (TXN_ID DECIMAL(23, 15), ITEM_ID INT)
	INSERT INTO #ITEM_PAIR
	SELECT A.TXN_ID, B.ITEM_ID FROM BA_Transactions A
		JOIN BA_Transactions B ON A.TXN_ID = B.TXN_ID
		WHERE A.ITEM_ID != B.ITEM_ID
			AND A.ITEM_ID = @ITEM

	
	-- LIFT
	---------------------------------------------------------------------------------------------------------------------
	IF OBJECT_ID('TEMPDB..#ACTUAL_OCCURANCE') IS NOT NULL
		DROP TABLE #ACTUAL_OCCURANCE
	CREATE TABLE #ACTUAL_OCCURANCE(ITEM_ID INT, NUMERATOR FLOAT)
	INSERT INTO #ACTUAL_OCCURANCE
	SELECT ITEM_ID, COUNT(DISTINCT TXN_ID)*1.0/@TOTAL_TXN_COUNT_ALL NUMERATOR FROM #ITEM_PAIR
			GROUP BY ITEM_ID
	
	-- ITEMS RANDOMLY OCCURING
	IF OBJECT_ID('TEMPDB..#ITEM_PROBABILITY') IS NOT NULL
		DROP TABLE #ITEM_PROBABILITY
	CREATE TABLE #ITEM_PROBABILITY (ITEM_ID INT, PROBABILITY FLOAT)
	INSERT INTO #ITEM_PROBABILITY
	SELECT ITEM_ID, COUNT(DISTINCT TXN_ID)*1.0/@TOTAL_TXN_COUNT_ALL PROBABILITY FROM BA_Transactions A
		GROUP BY ITEM_ID
	
	IF OBJECT_ID('TEMPDB..#LIFT') IS NOT NULL
		DROP TABLE #LIFT
	CREATE TABLE #LIFT (ITEM_ID INT, LIFT FLOAT)
	INSERT INTO #LIFT
	SELECT A.ITEM_ID, A.NUMERATOR/B.DENOMINATOR LIFT FROM #ACTUAL_OCCURANCE A
		JOIN (
			SELECT B.ITEM_ID, A.PROBABILITY * B.PROBABILITY DENOMINATOR FROM #ITEM_PROBABILITY A
				JOIN #ITEM_PROBABILITY B ON 1 = 1
				WHERE A.ITEM_ID != B.ITEM_ID
					AND A.ITEM_ID = @ITEM
			) AS B ON A.ITEM_ID = B.ITEM_ID
		ORDER BY 2 DESC
	

	
	---------------------------------------------------------------------------------------------------------------------
	SELECT A.ITEM_ID, C.INAME, C.IOPTCAT,  B.SUPPORT, B.CONFIDENCE, A.LIFT FROM #LIFT A
	LEFT JOIN (
		SELECT ITEM_ID, COUNT(DISTINCT TXN_ID)*1.0/@TOTAL_TXN_COUNT_ALL SUPPORT, COUNT(DISTINCT TXN_ID)*1.0/@TOTAL_TXN_COUNT CONFIDENCE FROM #ITEM_PAIR
			GROUP BY ITEM_ID
		) AS B ON A.ITEM_ID = B.ITEM_ID
	LEFT JOIN ITEMS C ON A.ITEM_ID = C.IITEM
		ORDER BY CONFIDENCE DESC